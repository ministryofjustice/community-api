plugins {
    id("uk.gov.justice.hmpps.gradle-spring-boot") version "4.0.1-beta"
    id 'groovy'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
    id "au.com.dius.pact" version "4.3.1"
}

group = 'uk.gov.justice.digital.delius'

testSets {
    testIntegration
}

check.dependsOn(testIntegration)

dependencies {

    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-ldap'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation("org.springframework.boot:spring-boot-starter-webflux")

    implementation 'com.unboundid:unboundid-ldapsdk:6.0.2'
    implementation("io.springfox:springfox-boot-starter:3.0.0")
    implementation 'org.flywaydb:flyway-core:8.3.0'
    implementation 'com.zaxxer:HikariCP:5.0.0'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'io.swagger:swagger-core:1.6.2'
    implementation 'com.oracle.database.jdbc:ojdbc10:19.12.0.0'
    implementation 'io.vavr:vavr:0.10.4'
    implementation 'com.github.java-json-tools:json-patch:1.13'

    // needed for record serialisation
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.5'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.5'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.5'

    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    compileOnly 'org.projectlombok:lombok:1.18.22'

    runtimeOnly 'com.h2database:h2:2.0.204'
    testRuntimeOnly 'com.h2database:h2:1.4.200'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    testCompileOnly 'org.projectlombok:lombok:1.18.22'

    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testImplementation ('io.rest-assured:rest-assured:4.4.0'){
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    testImplementation("au.com.dius.pact.provider:junit5spring:4.3.1")
    testImplementation 'io.rest-assured:spring-mock-mvc:4.4.0'
    testIntegrationImplementation "com.github.tomakehurst:wiremock:2.27.2"
    testImplementation 'io.swagger.parser.v3:swagger-parser-v2-converter:2.0.25'
}

testIntegration {
    println(project.getProperties().get("testFilter"))
    if (project.hasProperty("testFilter")) {
        List<String> props = project.getProperties().get("testFilter").split("\\s+")
        props.each {
            include(it.replace("src/testIntegration/java/", "**/").replace(".java", ".class"))
        }
    }
    timeout = Duration.ofMinutes(15)
    useJUnitPlatform ()
    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true
    }
    minHeapSize = "128m"
    maxHeapSize = "1536m"
    jvmArgs = ["--add-opens", "java.naming/com.sun.jndi.ldap=ALL-UNNAMED"]
}

repositories {
    mavenCentral()
}
