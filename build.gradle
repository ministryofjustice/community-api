import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.time.LocalDate
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '2.1.8.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id "com.gorylenko.gradle-git-properties" version "2.2.0"
    id 'com.github.ben-manes.versions' version '0.25.0'
}

project.sourceCompatibility = 1.8
project.targetCompatibility = 1.8

def today = Instant.now()
group = 'uk.gov.justice.digital.delius'

def todayDate = LocalDate.now()
def formatter = DateTimeFormatter.ofPattern('yyyy-MM-dd')
version todayDate.format(formatter)

jar {
    baseName = rootProject.name
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                'Implementation-Version': System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
            group = 'uk.gov.justice.digital.hmpps'
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

repositories {
    jcenter()
    mavenLocal()
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

dependencies {
    implementation files('lib/ojdbc8-12.2.0.1.jar')

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.10'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.10'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-ldap'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.hibernate:hibernate-java8:5.4.5.Final'
    implementation 'org.springframework.hateoas:spring-hateoas:0.25.2.RELEASE'
    implementation 'com.unboundid:unboundid-ldapsdk:4.0.11'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'org.flywaydb:flyway-core:5.2.4'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'com.zaxxer:HikariCP:3.4.1'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.5.0'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.5.0'
    implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.1.1'

    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    compileOnly 'org.projectlombok:lombok:1.18.10'

    testRuntime 'com.h2database:h2:1.4.199'
    runtime 'com.h2database:h2:1.4.199'

    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'junit:junit:4.12'
    testImplementation 'io.rest-assured:rest-assured:3.0.3'
    testImplementation "com.github.tomakehurst:wiremock:2.24.1"

}

test {
    timeout = Duration.ofMinutes(15)
    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true
    }
    minHeapSize = "128m"
    maxHeapSize = "1024m"
}
