plugins {
    id("uk.gov.justice.hmpps.gradle-spring-boot") version "4.1.2"
    id 'groovy'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
    id "au.com.dius.pact" version "4.3.6"
}

group = 'uk.gov.justice.digital.delius'

testSets {
    testIntegration
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
check.dependsOn(testIntegration)

dependencies {
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-ldap'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.oracle.database.jdbc:ojdbc11'
    implementation 'com.unboundid:unboundid-ldapsdk'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'org.flywaydb:flyway-core:8.5.5'
    implementation 'com.zaxxer:HikariCP'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'io.swagger:swagger-core:1.6.2'
    implementation 'io.vavr:vavr:0.10.4'
    implementation 'com.github.java-json-tools:json-patch:1.13'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    compileOnly 'org.projectlombok:lombok'

    runtimeOnly 'com.h2database:h2:2.1.210'
    testRuntimeOnly 'com.h2database:h2:1.4.200'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'

    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'io.rest-assured:spring-mock-mvc'
    testImplementation 'au.com.dius.pact.provider:junit5spring:4.3.6'
    testImplementation 'io.swagger.parser.v3:swagger-parser-v2-converter:2.0.25'
    testIntegrationImplementation 'com.github.tomakehurst:wiremock:2.27.2'
}

testIntegration {
    println(project.getProperties().get("testFilter"))
    if (project.hasProperty("testFilter")) {
        List<String> props = project.getProperties().get("testFilter").split("\\s+")
        props.each {
            include(it.replace("src/testIntegration/java/", "**/").replace(".java", ".class"))
        }
    }
    timeout = Duration.ofMinutes(15)
    useJUnitPlatform ()
    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true
    }
    minHeapSize = "128m"
    maxHeapSize = "1536m"
    jvmArgs = ["--add-opens", "java.naming/com.sun.jndi.ldap=ALL-UNNAMED"]
}

repositories {
    mavenCentral()
}
