import java.time.Instant
import java.time.LocalDate

plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id "com.gorylenko.gradle-git-properties" version "2.2.2"
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.13'
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    agentDeps
    agentDeps.transitive = false
}

project.sourceCompatibility = 11
project.targetCompatibility = 11

group = 'uk.gov.justice.digital.delius'

def today = Instant.now()
def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

jar {
    baseName = rootProject.name
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                'Implementation-Version': version
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version =  version
            group = 'uk.gov.justice.digital.hmpps'
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

dependencyManagement {
    imports {
        mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
    }
}

dependencies {
    implementation files('lib/ojdbc8-12.2.0.1.jar')

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.3'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-ldap'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.4.0.RELEASE'
    implementation 'org.springframework.security:spring-security-jwt:1.1.0.RELEASE'

    implementation 'org.hibernate:hibernate-java8:5.4.5.Final'
    implementation 'com.unboundid:unboundid-ldapsdk:4.0.13'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'org.flywaydb:flyway-core:6.3.3'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'com.zaxxer:HikariCP:3.4.2'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-lang3:3.10'
    implementation 'org.apache.commons:commons-text:1.8'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.6.0'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.6.0'
    agentDeps 'com.microsoft.azure:applicationinsights-agent:2.6.0'
    implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.2.1'
    implementation 'com.google.guava:guava:28.2-jre'

    implementation configurations.agentDeps

    implementation ('org.dom4j:dom4j:2.1.3') {
        because 'Transitive dependency. Previous versions have a known vulnerability'
    }
    implementation ('org.yaml:snakeyaml:1.26') {
        because 'Transitive dependency. Previous versions have a known vulnerability'
    }

    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    compileOnly 'org.projectlombok:lombok:1.18.12'

    testRuntime 'com.h2database:h2:1.4.200'
    runtime 'com.h2database:h2:1.4.200'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'

    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'junit:junit:4.13'
    testImplementation ('io.rest-assured:rest-assured:3.3.0'){
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    testImplementation 'io.rest-assured:spring-mock-mvc:3.3.0'
    testImplementation "com.github.tomakehurst:wiremock:2.26.3"
}

task copyAgent(type: Copy) {
  from configurations.agentDeps
  into "$buildDir/libs"
}

assemble.dependsOn copyAgent

test {
    timeout = Duration.ofMinutes(15)
    useJUnitPlatform ()
    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true
    }
    minHeapSize = "128m"
    maxHeapSize = "1024m"

}
